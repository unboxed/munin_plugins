#!/usr/bin/perl -w
# -*- perl -*-

=head1 NAME

snmp__readynas_df - Plugin to check disk usage of a Netgear ReadyNAS host via SNMP

=head1 CONFIGURATION

The following environment variables are used

 host      - SNMP hostname to probe (default undef)
 port      - SNMP port to use (default 161)
 community - SNMP community string (default "public")

=head1 AUTHOR

Copyright (C) 2011 Alex Tomlins <alex.tomlins@unboxedconsulting.com>

=head1 LICENSE

MIT License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

=head1 MAGIC MARKERS

 #%# family=snmpauto
 #%# capabilities=snmpconf

=cut

use strict;
use Munin::Plugin::SNMP;

my $DEBUG = 0;
my $MAXLABEL = 20;

my $hrStorageOid = "1.3.6.1.2.1.25.2.3.1";
my $hrStorageFixedDisk = "1.3.6.1.2.1.25.2.1.4";

if (defined $ARGV[0] and $ARGV[0] eq "snmpconf")
{
	print "index   $hrStorageOid.1.\n";
	print "require $hrStorageOid.2. $hrStorageFixedDisk\n"; # Type=fixed disk
	print "require $hrStorageOid.5. [1-9]\n"; # Size > 0
	exit 0;
}

my $session = Munin::Plugin::SNMP->session();
my $result = $session->get_hash(
		-baseoid => $hrStorageOid,
		-cols	 => {
			1 => 'index',
			2 => 'type',
			3 => 'desc',
			5 => 'size',
			6 => 'used',
		});

my %filesystems;
foreach (keys(%$result)) {
	my $part = $$result{$_};
	if ($$part{'type'} eq $hrStorageFixedDisk and $$part{'size'} > 0) {
		$filesystems{$_} = $part;
	}
}

if (defined $ARGV[0] and $ARGV[0] eq "config")
{
	(my $host) = Munin::Plugin::SNMP->config_session();
	print "host_name $host\n" unless $host eq 'localhost';
	print "graph_title Filesystem usage (in %)\n";
	print "graph_args --upper-limit 100 -l 0\n";
	print "graph_vlabel %\n";
	print "graph_category disk\n";
	print "graph_info This graph shows partition usage in percent.\n";

	foreach (sort keys(%filesystems)) {
		my $label = "p_$_";
		print "$label.label $filesystems{$_}{'desc'}\n";
		print ($label, ".warning 92\n");
		print ($label, ".critical 98\n");
		print ($label, ".info Usage for ". ($filesystems{$_}{'desc'})."\n");
	}
	exit 0;
}

foreach (sort keys(%filesystems)) {
	my $usage = $filesystems{$_}{'used'} * 100 / $filesystems{$_}{'size'};
	print "p_$_.value $usage\n";
}
