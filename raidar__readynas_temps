#!/usr/bin/perl -w
# -*- perl -*-

=head1 NAME

raidar__readynas_temps - Plugin to check temperatures of a Netgear ReadyNAS host using the RAIDar protocol

=head1 CONFIGURATION

The following environment variables are used

 port      - port to use (default 22081)

=head1 AUTHOR

Copyright (C) 2011 Alex Tomlins <alex.tomlins@unboxedconsulting.com>

=head1 LICENSE

MIT License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

=cut

use strict;
use Socket;

my $host;
if ($0 =~ /^(?:|.*\/)raidar_([^_]+)_/)
{
        $host  = $1;
}
my $port = $ENV{port}  || 22081;
my $timeout = 10.0;

my (%temps, %temp_limits, %disks);

my $stats = get_raw_stats();
foreach (split(/\n/, $stats)) {
	if ($_ =~ /^temp!!(\d+)!!.*descr=([\d.]+)C.*expected=\d+-(\d+)C/) {
		$temps{$1} = $2;
		$temp_limits{$1} = $3;
	}
	if ($_ =~ /^disk!!(\d+)!!.*\s(\d+)C\/\d+F$/) {
		$disks{$1} = $2;
	}
}

if (defined $ARGV[0] and $ARGV[0] eq "config")
{
	print "host_name $host\n" unless $host eq 'localhost';
	print "graph_title Temperatures\n";
	print "graph_args -l 21\n";
	print "graph_vlabel Celsius\n";
	print "graph_category sensors\n";

	foreach (sort(keys(%temps))) {
		print "temp_$_.label Temp $_\n";
		print "temp_$_.warning $temp_limits{$_}\n";
	}
	foreach (sort(keys(%disks))) {
		print "disk_$_.label Disk $_\n";
		print "disk_$_.warning 50\n";
	}

	exit 0;
}

foreach (sort(keys(%temps))) {
	print "temp_$_.value $temps{$_}\n";
}
foreach (sort(keys(%disks))) {
	print "disk_$_.value $disks{$_}\n";
}

sub get_raw_stats {
	my $msg = chr(0x00).chr(0x00).chr(0x05).chr(0xd3).chr(0x00).chr(0x00).
	          chr(0x00).chr(0x01).chr(0x00).chr(0x00).chr(0x00).chr(0x00).
	          chr(0x80).chr(0xc9).chr(0x6c).chr(0x05).chr(0xff).chr(0xff).
	          chr(0xff).chr(0xff).chr(0x00).chr(0x00).chr(0x00).chr(0x1c).
	          chr(0x00).chr(0x00).chr(0x00).chr(0x00);
	socket(SOCKET, PF_INET, SOCK_DGRAM, getprotobyname("udp")) or die "socket: $!";
	$| = 1;
	my $ip_addr = inet_aton($host);
	send(SOCKET, $msg, 0, sockaddr_in($port, $ip_addr)) == length($msg) or die "send: $!";
	my ($rin, $rout);
	$rin = '';
	vec($rin, fileno(SOCKET), 1) = 1;
	my $data = '';
	while (select($rout = $rin, undef, undef, $timeout)) {
		my $chunk = '';
		recv(SOCKET, $chunk, 1458, 0) or die "recv: $!";
		$data = substr($chunk, 28);
		last if length($data) > 0;
	}
	return $data;
}
